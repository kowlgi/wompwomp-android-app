buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

//apply plugin: 'com.android.databinding'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"
    defaultConfig {
        applicationId 'co.wompwomp.sunshine'
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 6
        versionName "1.1" + "." + versionCode
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "app_name", "wompwomp"
        resValue "string", "app_version", versionName
        resValue "string", "authorities", applicationId
        resValue "string", "account_type", applicationId + ".SyncAdapter"
        buildConfigField "String", "ACCOUNT_TYPE", "\"co.wompwomp.sunshine.SyncAdapter\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".release"
            resValue "string", "app_version", defaultConfig.versionName + ".release"
        }
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
            resValue "string", "app_name", "wompwomp-sssh"
            resValue "string", "app_version", defaultConfig.versionName + ".debug"
            resValue "string", "authorities", defaultConfig.applicationId + '.debug'
            resValue "string", "account_type", defaultConfig.applicationId + ".SyncAdapter.debug"
            buildConfigField "String", "ACCOUNT_TYPE", "\"co.wompwomp.sunshine.SyncAdapter.debug\""
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.4@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:percent:23.1.0'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.google.android.gms:play-services-gcm:8.1.0'
    compile 'com.ocpsoft:ocpsoft-pretty-time:1.0.6'
    compile 'joda-time:joda-time:2.8.2'
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'
    compile 'com.jakewharton.timber:timber:4.1.0'
}
