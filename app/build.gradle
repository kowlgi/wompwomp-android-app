buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.3'
    defaultConfig {
        applicationId 'co.wompwomp.sunshine'
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 12
        versionName "1.3.3"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        resValue "string", "app_name", "wompwomp"
        resValue "string", "app_version", versionName
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            versionNameSuffix ".release"
            resValue "string", "app_version", defaultConfig.versionName + ".release"
            resValue "string", "authorities", defaultConfig.applicationId
            resValue "string", "account_type", defaultConfig.applicationId + ".SyncAdapter"
            resValue "string", "facebook_provider_authorities", "com.facebook.app.FacebookContentProvider570524353097440"
            buildConfigField "String", "ACCOUNT_TYPE", "\"co.wompwomp.sunshine.SyncAdapter\""
        }
        debug {
            ext.enableCrashlytics = false
            applicationIdSuffix ".debug"
            versionNameSuffix ".debug"
            resValue "string", "app_name", "wompwomp-sssh"
            resValue "string", "app_version", defaultConfig.versionName + ".debug"
            resValue "string", "authorities", defaultConfig.applicationId + '.debug'
            resValue "string", "account_type", defaultConfig.applicationId + ".SyncAdapter.debug"
            resValue "string", "facebook_provider_authorities", "com.facebook.app.FacebookContentProvider570524353097440.debug"
            buildConfigField "String", "ACCOUNT_TYPE", "\"co.wompwomp.sunshine.SyncAdapter.debug\""
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    output.outputFile.name.replace(".apk", "-${variant.versionName}.apk"))
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true;
    }
    compile('com.crashlytics.sdk.android:answers:1.3.4@aar') {
        transitive = true;
    }
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.4-beta2'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.4-beta2'
    compile 'com.android.support:appcompat-v7:23.3.0'
    compile 'com.android.support:support-v4:23.3.0'
    compile 'com.android.support:design:23.3.0'
    compile 'com.android.support:recyclerview-v7:23.3.0'
    compile 'com.android.support:cardview-v7:23.3.0'
    compile 'com.google.android.gms:play-services-analytics:8.4.0'
    compile 'com.google.android.gms:play-services-gcm:8.4.0'
    compile 'org.ocpsoft.prettytime:prettytime:4.0.1.Final'
    compile 'net.danlew:android.joda:2.9.3'
    compile 'com.jakewharton.timber:timber:4.1.2'
    compile 'com.facebook.android:facebook-android-sdk:4.11.0'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    compile 'com.plattysoft.leonids:LeonidsLib:1.3.1'
    compile 'com.google.android.exoplayer:exoplayer:r1.5.7'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.squareup.okio:okio:1.8.0'
    compile 'com.facebook.stetho:stetho:1.3.1'
    compile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
}

apply plugin: 'com.google.gms.google-services'
